field = { (ASCII_DIGIT | "." | "-")+ }

stil_source = { SOI ~ stil_source_items+ ~ EOI }

stil_source_items = {
    S | N | stil_version  | header_block | signals_block | signal_groups_block |
    timing_block | pattern_exec_block | pattern_burst_block | spec_block | selector_block |
    scan_structures_block | pattern_block
}

stil_version = { "STIL" ~ ASCII_DIGIT ~ "." ~ ASCII_DIGIT ~ EOS }

//############################################################################
//# 6.13 and 6.14. Timing and Signal Expressions
//############################################################################

sigref_expr = { name | (single_quote ~ expression_subset ~ single_quote) }
expression_subset = { add | subtract | name | paren_expression }
expression = { add | subtract | multiply | divide | number_with_unit | number | name | paren_expression }
terminal = { number_with_unit | number | name | paren_expression }
add = { terminal ~ "+" ~ expression }
subtract = { terminal ~ "-" ~ expression }
multiply = { terminal ~ "*" ~ expression }
divide = { terminal ~ "/" ~ expression }
paren_expression = { "(" ~ expression ~ ")" }
time_expr = { single_quote ~ expression ~ single_quote }
number_with_unit = { number+ ~ engineering_prefix? ~ si_unit }
si_unit = { "A" | "Cel" | "F" | "H" | "Hz" | "m" | "Ohm" | "s" | "W" | "V" }
engineering_prefix = { "E" | "P" | "T" | "G" | "M" | "k" | "m" | "u" | "n" | "p" | "f" | "a" }

//############################################################################
//# 9. Header Block
//############################################################################

header_block = { "Header" ~ "{" ~ title? ~ date? ~ source? ~ history? ~ "}" }
title = { "Title" ~ string ~ EOS }
date = { "Date" ~ string ~ EOS }
source = { "Source" ~ string ~ EOS }
history = { "History" ~ "{" ~ annotation* ~ "}" }
annotation = { "Ann" ~ "{*" ~ (!"*}" ~ ANY)* ~ "*}" }

//############################################################################
//# 14. Signals Block
//############################################################################

signals_block = { "Signals" ~ "{" ~ signal* ~ "}" }
signal = {
    (name ~ ("InOut" | "Out" | "In" | "Supply" | "Pseudo") ~ EOS) |
    (name ~ ("InOut" | "Out" | "In" | "Supply" | "Pseudo") ~
    ("{" ~ termination? ~ default_state? ~ base? ~ alignment? ~ scan_in? ~ scan_out? ~ data_bit_count? ~ "}" ))
}

termination = {
    "Termination" ~ ("TerminateHigh" | "TerminateLow" | "TerminateOff" | "TerminateUknown") ~ EOS
}

default_state = {
    "DefaultState" ~ ("U" | "D" | "Z" | "ForceUp" | "ForceDown" | "ForceOff") ~ EOS
}

base = { "Base" ~ ("Hex" | "Dec") ~ waveform_character_list ~ EOS }

alignment = { "Alignment" ~ ("MSB" | "LSB") ~ EOS }
scan_in = { "ScanIn" ~ integer ~ EOS }
scan_out = { "ScanOut" ~ integer ~ EOS }
data_bit_count = { "DataBitCount" ~ integer ~ EOS }
waveform_character_list = @{ ASCII_ALPHANUMERIC+ }

//############################################################################
//# 15. SignalGroups Block
//############################################################################

signal_groups_block = { "SignalGroups" ~ name? ~ "{" ~ signal_group* ~ "}" }
signal_group = {
    (name ~ "=" ~ sigref_expr ~ EOS) |
    (name ~ "=" ~ sigref_expr ~ 
       ("{" ~ termination? ~ default_state? ~ base? ~ alignment? ~ scan_in? ~ scan_out? ~ data_bit_count? ~ "}" ))
}

//############################################################################
//# 16. PatternExec Block
//############################################################################

pattern_exec_block = {
    "PatternExec" ~ name? ~ "{" ~ category* ~ selector* ~ timing? ~ pattern_burst? ~ "}"
}
category = { "Category" ~ name ~ EOS }
selector = { "Selector" ~ name ~ EOS }
timing = { "Timing" ~ name ~ EOS }
pattern_burst = { "PatternBurst" ~ name ~ EOS }

//############################################################################
//# 17. PatternBurst Block
//############################################################################

pattern_burst_block = {
    "PatternBurst" ~ name ~ "{" ~ signal_groups* ~ macro_defs* ~ procedures? ~ scan_structures? ~ start? ~
        stop? ~ termination_block* ~ pat_list+ ~ "}"
}
signal_groups = { "SignalGroups" ~ name ~ EOS }
macro_defs = { "MacroDefs" ~ name ~ EOS }
procedures = { "Procedures" ~ name ~ EOS }
scan_structures = { "ScanStructures" ~ name ~ EOS }
start = { "Start" ~ name ~ EOS }
stop = { "Stop" ~ name ~ EOS }
termination_block = { "Termination" ~ "{" ~ termination_item* ~ "}" }
termination_item = {
    sigref_expr ~ ("TerminateHigh" | "TerminateLow" | "TerminateOff" | "TerminateUknown") ~ EOS
}
pat_list = { "PatList" ~ "{" ~ pat_list_item* ~ "}" }
pat_list_item = {
    name ~ (EOS | ("{" ~ signal_groups* ~ macro_defs* ~ procedures? ~ scan_structures? ~ start? ~
                       stop? ~ termination_block* ~ "}"))
}

//############################################################################
//# 18. Timing block
//############################################################################

timing_block = { "Timing" ~ name? ~ "{" ~ signal_groups_ref* ~ waveform_table* ~ "}" }
signal_groups_ref = { "SignalGroups" ~ name ~ EOS }
waveform_table = {
    "WaveformTable" ~ name ~ "{" ~ period? ~ inherit_waveform_table* ~ sub_waveforms? ~ waveforms ~ "}"
}
period = { "Period" ~ time_expr ~ EOS }
inherit_waveform_table = { "InheritWaveformTable" ~ name ~ EOS }
inherit_waveform = { "InheritWaveform" ~ name ~ EOS }
inherit_waveform_wfc = { "InheritWaveform" ~ name_wfc ~ EOS }
name_wfc = { ((name_segment ~ ".")+ ~ wfc_list) | wfc_list }
sub_waveforms = { "SubWaveforms" } // Not implemented yet
waveforms = { "Waveforms" ~ "{" ~ waveform* ~ "}" }
waveform = { sigref_expr ~ "{" ~ inherit_waveform* ~ wfc_definition* ~ "}" }
wfc_char = { ASCII_ALPHANUMERIC }
wfc_list = @{ wfc_char+ }
wfc_definition = { wfc_list ~ "{" ~ inherit_waveform_wfc* ~ event* ~ "}" }
event = { event_label? ~ time_expr? ~ event_list? ~ EOS }
event_label = { name ~ ":" }
event_char = {
    "D" | "U" | "Z" | "P" | "L" | "H" | "X" | "x" | "T" | "V" | "l" | "h" | "t" | "v" | "R" | "G" |
    "Q" | "M" | "N" | "A" | "B" | "F" | "?"
}
event_list = { event_char ~ ("/" ~ event_char)* }

//############################################################################
//# 19. Spec and Selector Blocks
//############################################################################

spec_block = { "Spec" ~ name? ~ "{" ~ (category_block | variable_block)* ~ "}" }
category_block = { "Category" ~ name ~ "{" ~ spec_item* ~ "}" }
spec_item = { typical_var | spec_var }
typical_var = { name ~ "=" ~ time_expr ~ EOS }
spec_var = { name ~ "{" ~ spec_var_item* ~ "}" }
spec_var_item = { ("Min" | "Typ" | "Max") ~ time_expr ~ EOS }
variable_block = { "Variable" ~ name ~ "{" ~ spec_item* ~ "}" }
selector_block = { "Selector" ~ name ~ "{" ~ selector_item* ~ "}" }
selector_item = @{ name ~ S ~ ("Min" | "Typ" | "Max" | "Meas") ~ s ~ EOS }

//############################################################################
//# 20. ScanStructures Blocks
//############################################################################

scan_structures_block = { "ScanStructures" ~ name? ~ "{" ~ scan_chain* ~ "}" }
scan_chain = { "ScanChain" ~ name ~ "{" ~ scan_length ~ (scan_out_length | scan_cells | scan_in_name |
               scan_out_name | scan_master_clock | scan_slave_clock | scan_inversion)* ~ "}" }
scan_length = { "ScanLength" ~ integer ~ EOS }
scan_out_length = { "ScanOutLength" ~ integer ~ EOS }
scan_cells = { "ScanCells" ~ ("!" | name)* ~ EOS }
scan_in_name = { "ScanIn" ~ name ~ EOS }
scan_out_name = { "ScanOut" ~ name ~ EOS }
scan_master_clock = { "ScanMasterClock" ~ name* ~ EOS }
scan_slave_clock = { "ScanSlaveClock" ~ name* ~ EOS }
scan_inversion = { "ScanInversion" ~ ("0" | "1") ~ EOS }

//############################################################################
//# 23. Pattern Blocks
//############################################################################

pattern_block = { "Pattern" ~ name ~ "{" ~ (label? ~ time_unit)? ~ pattern_statement* ~ "}" }
label = { (string | name) ~ ":" }
time_unit = { "TimeUnit" ~ time_expr ~ EOS }
pattern_statement = { label | vector | waveform_statement | condition | call | macro_statement | loop_statement |
                      match_loop | goto | breakpoint | iddq | stop_statement | scan_chain_statement | annotation }

vector = { "V" ~ "ector"? ~ "{" ~ (cyclized_data | non_cyclized_data)* ~ "}" }
cyclized_data = { (sigref_expr ~ "=" ~ vec_data+ ~ EOS) | (sigref_expr ~ "{" ~ (vec_data ~ EOS)+ ~ "}") }
vec_data = { repeat | waveform_format | hex_format | dec_format | data_string }
repeat = @{ "\\r" ~ integer }
waveform_format = { "\\w" }
hex_format = @{ "\\h " | ("\\h" ~ ASCII_ALPHANUMERIC+) }
dec_format = @{ "\\d " | ("\\d" ~ ASCII_ALPHANUMERIC+) }
data_string = { ASCII_ALPHANUMERIC+ }
non_cyclized_data = { (time_value ~ sigref_expr ~ "=" ~ event_list ~ EOS) |
                      (time_value ~ "{" ~ (sigref_expr ~ "=" ~ event_list ~ EOS)+ ~ "}") }
time_value = @{ "@" ~ integer }

waveform_statement = { "W" ~ "aveformTable"? ~ name ~ EOS }

condition = { "C" ~ "ondition"? ~ "{" ~ (cyclized_data | non_cyclized_data)* ~ "}" }

//call = { ("Call" ~ name ~ "{" ~ (scan_data | cyclized_data | non_cyclized_data)* ~ "}") | ("Call" ~ name ~ EOS) }
// Can't see what is different between scan_data and cyclized_data
call = { ("Call" ~ name ~ "{" ~ (cyclized_data | non_cyclized_data)* ~ "}") | ("Call" ~ name ~ EOS) }

macro_statement = { ("Macro" ~ name ~ "{" ~ (cyclized_data | non_cyclized_data)* ~ "}") | ("Macro" ~ name ~ EOS) }

loop_statement = { "Loop" ~ integer ~ "{" ~ pattern_statement* ~ "}" }

match_loop = { "MatchLoop" ~ (integer | "Infinite") ~ "{" ~ pattern_statement+ ~ "}" }

goto = { "Goto" ~ name ~ EOS }

breakpoint = { ("BreakPoint" ~ EOS) | ("BreakPoint" ~ "{" ~ pattern_statement* ~ "}") }

iddq = { "IDDQ" ~ "TestPoint" ~ EOS }

stop_statement = { "Stop" ~ EOS }

scan_chain_statement = { "ScanChain" ~ name ~ EOS }

//############################################################################
//# Identifiers
//############################################################################

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
name = @{ ((name_segment ~ ".")+ ~ name_segment) | name_segment }
name_segment = { simple_identifier | escaped_identifier }
simple_identifier = @{ (letter | underline) ~ simple_character* }
simple_character = { letter | digit | underline }
letter = { upper_case_letter | lower_case_letter }
upper_case_letter = { ASCII_ALPHA_UPPER }
lower_case_letter = { ASCII_ALPHA_LOWER }
underline = { "_" }
escaped_identifier = @{ "\"" ~ escaped_character+ ~ "\"" }
escaped_character = { simple_character | special_character | whitespace_character }
special_character = {
    "!" | "@" | "#" | "$" | "%" | "^" | "&" | "*" | "(" | ")" | "-" | "+" |
    "+" | "|" | "`" | "~" | "{" | "[" | "}" | "]" | ":" | ";" | single_quote | "," |
    "<" | "." | ">" | "/" | "?" | "\\"
}
whitespace_character = { " " | "\t" | "\n" }

//#############################################################################
//# Numbers
//#############################################################################

digit = { ASCII_DIGIT }
hexdigit = { ASCII_HEX_DIGIT }
hex_number = @{ hexdigit+ }
integer = @{ digit+ }
signed_integer = @{ integer | ("-" ~ integer) }
number = {
    (signed_integer ~ "." ~ integer ~ "e" ~ signed_integer) |
    (signed_integer ~ "." ~ integer) | (signed_integer ~ "e" ~ signed_integer) |
    signed_integer
}

//#############################################################################
//# Comments and Whitespace
//#############################################################################
EOS = _{ ";" }

single_quote = _{ "'" | "â€™" }

block_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

one_line_comment = _{ "//" ~ (!N ~ ANY)* }

space = _{ " " | "\t" }

// Optional space, including newlines and comments
s = _{ (space | N | one_line_comment | block_comment)* }

// Required space, including newlines and comments
S = _{ (space | N | one_line_comment | block_comment)+ }

// Optional end of line
n = _{ NEWLINE? }

// Required end of line
N = _{ NEWLINE }

// For Pest implicit whitespace insertion
WHITESPACE = _{ space | NEWLINE }
COMMENT = _{ block_comment | one_line_comment }